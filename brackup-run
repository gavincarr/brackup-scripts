#!/usr/bin/perl
#
# Brackup wrapper script, supporting (optionally) snapshots, monthly backups,
# and pre- and post-run scripts.
#

use strict;
use Getopt::Long qw(:config no_ignore_case bundling);
use File::Basename;
use Config::Tiny;
use DateTime;
use LWP::UserAgent;

sub usage {
  die "usage: " . basename($0) . " [-v] [-n] [-t <target>] [-d <run_dir>] [--monthly] [-u <murl>] [--snapshot] <source> [<source2>...]\n";
}

my $run_dir = '/root/brackup';
my ($help, $noop, $verbose, $snapshot, $monthly, $monthly_url, $target);
usage unless GetOptions(
  'help|h|?'            => \$verbose,
  'noop|n'              => \$noop,
  'verbose|v'           => \$verbose,
  'dir|run-dir|d=s'     => \$run_dir,
  'snapshot|s!'         => \$snapshot,
  'monthly|m!'          => \$monthly,
  'monthly-url|u=s'     => \$monthly_url,
  'target|t=s'          => \$target,
);
usage if $help;
usage unless @ARGV;

$verbose = '-v' if $verbose;
$< == 0        or die "Must run as root - aborting";
chdir $run_dir or die "Cannot cd to $run_dir";

# Load config file, if available
my $config = Config::Tiny->read('brackup-run.conf') if -f 'brackup-run.conf';
if ($config) {
  $monthly          = $config->{_}->{monthly}      if ! defined $monthly;
  $snapshot         = $config->{_}->{snapshot}     if ! defined $snapshot;
  $monthly_url      = $config->{_}->{monthly_url}  if ! defined $monthly_url;
  die "monthly_url is not set, required in monthly mode\n" 
    if $monthly and ! $monthly_url;
  $monthly_url =~ s! /$ !!x if $monthly_url;
}

my ($ua, $month);
if ($monthly) {
  $ua = LWP::UserAgent->new;
  $month = DateTime->today->strftime('%Y%m');
}

if ($verbose) {
  printf STDERR "+ run_dir: %s\n",      $run_dir;
  printf STDERR "+ snapshot: %s\n",     $snapshot || '';
  printf STDERR "+ monthly: %s\n",      $monthly || '';
  printf STDERR "+ monthly_url: %s\n",  $monthly_url || '';
  printf STDERR "+ month: %s\n",        $month || '' if $monthly;
}

for my $source (@ARGV) {
  printf STDERR "+ [ starting brackup of source '$source' ]\n" if $verbose;

  $target = $config->{$source}->{target} || $config->{_}->{target} if ! defined $target;
  die "Required parameter 'target' is not set for source '$source'\n" unless $target;
  printf STDERR "+ target: %s\n", $target || '' if $verbose;

  my $murl = "$monthly_url/month-$source";
  printf STDERR "+ murl: %s\n", $murl if $verbose;
  my $monthly_update = 0;
  if ($monthly) {
    # Check if we should do a monthly backup
    my $resp = $ua->get( $murl );
    if (! $resp->is_success || $month > $resp->decoded_content) {
      $monthly_update++;
      $source .= '-monthly';
    }
  }

  # Run brackup
  my $cmd = "/usr/bin/brackup --from=$source --to=$target --save-stats $verbose";
  print STDERR "+ cmd: $cmd\n" if $verbose;
  unless ($noop) {
    system($cmd);

    # Update monthly state file if all okay
    if ($? == 0 && $monthly_update) {
      $ua->request( PUT($murl, Content => "$month\n") )
        or die "PUT to $murl failed: $!";
    }
  }
}

